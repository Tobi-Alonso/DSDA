--unit control

--includes
	library ieee;
	use ieee.std_logic_1164.all;

	entity ControlUnit is
	  port (
		clock: 	in std_logic;
		rst: 	in std_logic;
		--interfase to sistem
		rd:		in std_logic;
		wr:		in std_logic;
		eoop:	out std_logic;
		--controler signals
		LatchAddr_L:	out std_logic;
		DR_LD:		out std_logic;
		LD_S_M: 		out std_logic;
		DR_O2M:		out std_logic;
		DR_O2S:		out std_logic;

		-- memory control output
		CE_L:			out std_logic;
		WE_L:			out std_logic;
		OE_L:			out std_logic;
		UB_L:			out std_logic;
		LB_L:			out std_logic
	  ) ;
	end entity ; -- ControlUnit
	
	architecture arch of ControlUnit is
		type controler_states is (IDLE,RD1,RD2,RD3,WR1,WR2);	
		signal current_state: controler_states;
		signal next_state: controler_states;
	
	begin
		current_state_logic : process( clock )
		begin
			if( rising_edge(clock) ) then
				current_state<=next_state;
			end if ;
		end process ; -- current_state_logic
		
		next_state_logic : process( current_state,rd,wr,rst)
		begin
			if rst='1' then
				next_state<=IDLE;
			else
				case( current_state ) is	
					when IDLE =>
							if wr='1' then
								next_state<=WR1;
							elsif rd='1' then
								next_state<=RD1;
							else
								next_state<=IDLE;
							end if ;
					when RD1 =>	next_state<= RD2;
					when RD2 =>	next_state<=RD3;
					when RD3 =>
						if (rd='1') then
							next_state<= RD3;
						else
							next_state<=IDLE;
						end if ;
					when WR1 => next_state<=WR2;						
					when WR2 => next_state<=IDLE;	
				end case ;						
			end if ;
		end process ; -- next_state_logic

			
		output_logic : process( current_state,rd,wr )
		begin
				eoop<='1';			
				LatchAddr_L<='0';	
				DR_LD<='1';		
				LD_S_M<='0'; 		
				DR_O2M<='0';		
				DR_O2S<='0';		

				-- memory control output
				CE_L<='1';			
				WE_L<='1';			
				OE_L<='1';			
				UB_L<='0';			
				LB_L<='0';	
				
			case( current_state ) is
				when IDLE => 
								if (wr='0' and rd='0') then
									LatchAddr_L<='1';		--Mealy output to set ffd outside and avoid glitches			
								end if ;
				when RD1=> 	OE_L<='0';
								CE_L<='0';
								eoop<='0';
								LD_S_M<='1';

				when RD2=>	DR_LD<='0';
								DR_O2S<='1';
								OE_L<='0';
								CE_L<='0';
				when RD3=>	DR_O2S<='1';
								DR_LD<='0';
								if rd='0' then
									LatchAddr_L<='1';	
								end if ;
				when WR1=>	eoop<='0';
								CE_L<='0';
								WE_L<='0';
								DR_LD<='0';
				when WR2=>	DR_LD<='0';
								CE_L<='0';
								WE_L<='0';
								eoop<='0';
								DR_O2M<='1';
								LatchAddr_L<='1';	
			end case ;


		end process ; -- output_logic

			



	end architecture ; -- arch